apply plugin: 'groovy'

group = 'se.transmode.gradle'

buildscript {
    // inspired by https://github.com/Netflix/gradle-template
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

repositories {
    mavenCentral()
}

apply from: file('gradle/license.gradle')
apply from: file('gradle/idea.gradle')
apply from: file('gradle/maven.gradle')

def getVersionName() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--long'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
version = getVersionName()

sourceSets {
    integTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        groovy {
            srcDir 'src/integTest/groovy'
        }
        resources {
            srcDir 'src/integTest/resources'
        }
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'junit:junit-dep:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

task integTest(type: Test) {
    group = 'verification'
    description = 'Runs the integration tests.'
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath

}

// run integration tests after unit tests if both are scheduled for execution
integTest.mustRunAfter test

check.dependsOn(integTest)

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}
